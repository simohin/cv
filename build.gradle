plugins {
    id 'org.springframework.boot' version '2.6.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.vaadin' version '23.0.2'
    id 'java'
}

group = 'com.simohin'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven {
        url('https://maven.vaadin.com/vaadin-addons')
    }
}

ext {
    set('vaadinVersion', '23.0.2')
    set('springBootVersion', '2.6.5')
    set('lombokVersion', '1.18.22')
    set('componentToggleButtonVersion', '1.0.1')
    set('firebaseAdminVersion', '8.1.0')
    set('googleApiClientVersion', '1.33.4')
    set('googleOauthClientJettyVersion', '1.33.1')
    set('googleApiServicesCalendarVersion', 'v3-rev20220203-1.32.1')
}
tasks {
    jar.enabled = false
    bootJar.enabled = true
}
vaadin {
    productionMode = System.getenv("VAADIN_PROD_MODE")
}

dependencies {
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-devtools'

    implementation "com.google.firebase:firebase-admin:$firebaseAdminVersion"

    implementation "com.google.api-client:google-api-client:$googleApiClientVersion"
    implementation "com.google.oauth-client:google-oauth-client-jetty:$googleOauthClientJettyVersion"
    implementation "com.google.apis:google-api-services-calendar:$googleApiServicesCalendarVersion"

    implementation "com.vaadin.componentfactory:togglebutton:$componentToggleButtonVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:$vaadinVersion"
    }
}

test {
    useJUnitPlatform()
}
